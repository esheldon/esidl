PRO binradec, run1, run2, clr, struct, $
              step0=step0, stepadd=stepadd, nstep=nstep,$
              map=map, $
              get=get, $
              star=star,$
              hist=hist, $
              dir=dir

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;+
;
; NAME:
;       
; PURPOSE:
;	
;
; CALLING SEQUENCE:
;      
;                 
;
; INPUTS: 
;
; OPTIONAL INPUTS:
;
; KEYWORD PARAMETERS:
;
;       
; OUTPUTS: 
;
; OPTIONAL OUTPUTS:
;
; CALLED ROUTINES:
; 
; PROCEDURE: 
;	
;	
;
; REVISION HISTORY:
;	
;       
;                                      
;-                                       
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


  IF N_params() LT 3 THEN BEGIN 
      print,'-Syntax: binradec, run1, run2, clrindex [, struct, step0=step0, stepadd=stepadd, nstep=nstep, map=map, get=get, stars=stars, hist=hist]'
      print,''
      print,'Use doc_library,"binradec"  for more help.'  
      return
  ENDIF 
  
  pold=!p.multi
  !p.multi=[0,1,2]

  tt = systime(1)

  COMMON par, minra,maxra,mindec,maxdec,radiff,decdiff



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Some parameters
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  ;; Defaults
  ;; side of the initial binsize in degrees. 
  ;; .2276 = side of frame in column directon.  But: overlap reduces
  ;; what we can fit.  For 12 across use .21
  IF n_elements(step0) EQ 0 THEN step0 =  0.21     
  IF n_elements(nstep) EQ 0 THEN nstep = 11
  IF n_elements(stepadd) EQ 0 THEN stepadd = step0

;  step0 = .1  ; 1 square degree on 10th bin
;  stepadd = step0  ; how much to increase binsize by each time
;  nstep = 26

  IF keyword_set(map) THEN map=1 ELSE map=0
  IF keyword_set(get) THEN get=1 ELSE get=0
  IF keyword_set(hist) THEN hist = 1 ELSE hist = 0
  IF keyword_set(star) THEN star=1 ELSE star=0
  IF NOT keyword_set(dir) THEN dir = '/sdss4/data1/esheldon/CORRECTED/'
  IF star THEN nnm='star' ELSE nnm='srcgal'
  imdir = dir+'SHAPEIM/'+nnm+'/'

  colors=['u','g','r','i','z']

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Get the 2 interleaved runs
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  r1str = ntostr(run1)
  r2str = ntostr(run2)

  IF get THEN BEGIN
      sname=dir+'run'+r1str+'_'+r2str+'_'+nnm+'_'+colors[clr]+'_overlap.fit'
      IF NOT exist(sname) THEN $
        sname=dir+'run'+r2str+'_'+r1str+'_'+nnm+'_'+colors[clr]+'_overlap.fit'
      IF NOT exist(sname) THEN BEGIN
          print,'No overlap file exists for these two runs'
          return
      ENDIF 
      struct = mrdfits(sname, 1, hdr)
  ENDIF 
  minra = min(struct.ra)
  maxra = max(struct.ra)
  mindec = min(struct.dec)
  maxdec = max(struct.dec)
  radiff  = maxra - minra
  decdiff = maxdec - mindec

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Open the files
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  prename = 'run'+r1str+'_'+r2str+'_'+nnm+'_'+colors[clr]+'_bin'

  i=1
  istr = '_'+ntostr(i)
  psname = dir+prename+istr+'.ps'
  dataname = dir+prename+istr+'.dat'
  WHILE exist(psname) DO BEGIN 
      istr = '_'+ntostr(i)
      psname = dir+prename+istr+'.ps'
      dataname = dir+prename+istr+'.dat'
      i = i+1
  ENDWHILE 
      
  print, 'postscript file: ',psname
  print, 'data file:       ',dataname

  logname = dir+'log.txt'
  openw, lun1, logname, /get_lun

  openw, lun2, dataname, /get_lun
  !textunit = lun2


  printf, lun1, '  #bins  sq.deg/bin  sq.deg.  e1err   e2err  nobj/bin'
  printf, lun1, '-------------------------------------------------------'

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Bin by various sizes. 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  nn = n_elements(struct)
  FOR ci = 0, nstep-1 DO BEGIN 

      step = step0 + ci*stepadd

      ;; Number of bins in each direction
      nra = long( radiff/step ) > 1
      ndec = long( decdiff/step ) > 1

      ;; bin area in sq. deg.
      ba = step^2

      ;; total no. of bins.
      nb = nra*ndec

      ;; total area used
      ta = nb*ba

      ;; Create an image of the shape
      ime1 = 0.
      ime2 = 0.
      ime  = 0.
      imnum = 0.
      imradec = 0.
      ime1 = dblarr(ndec, nra)
      ime2 = dblarr(ndec, nra)
      ime  = dblarr(ndec, nra)
      imnum = lonarr(ndec, nra)
      imra = dblarr(ndec, nra)
      imdec = dblarr(ndec, nra)

      print
      print,ntostr(ci+1)+'/'+ntostr(nstep)
      print,ntostr(nra)+' dots'

      ; Get averages in each bin in both directions
      ss = lindgen(nn)
      FOR ira = 0L, nra-1 DO BEGIN 
          print, format='($,A)', '.'
          ra1 = minra + ira*step
          ra2 = minra + (ira+1.)*step

          ; Faster to do where on ra first

          wt = where( struct[ss].ra GE ra1 AND struct[ss].ra LE ra2, nwt)
          w1 = ss[wt]

                                ;So it will go faster and faster
          remove, wt, ss
          FOR idec = 0L, ndec-1 DO BEGIN 

              dec1 = mindec + idec*step
              dec2 = mindec + (idec+1.)*step

              w2 = where(struct[w1].dec GE dec1 AND $
                         struct[w1].dec LE dec2, tnobj)
              w = w1[w2]
                                ; take .32 as intrinsic spread in weight
              spread = .32
        
              wmom, struct[w].e1, sqrt( spread^2 + struct[w].uncert^2 ), $
                te1, te1sig, te1err, te1sigerr, te1errerr
              wmom, struct[w].e2, sqrt( spread^2 + struct[w].uncert^2 ), $
                te2, te2sig, te2err, te2sigerr, te2errerr

              IF ira EQ 0 AND idec EQ 0 THEN BEGIN 
                  be1 = te1
                  be2 = te2
                  be1sig = te1sig
                  be2sig = te2sig
                  be1err = te1err
                  be2err = te2err
                  be1sigerr = te1sigerr
                  be2sigerr = te2sigerr
                  be1errerr = te1errerr
                  be2errerr = te2errerr
          
                  nobj = tnobj
              ENDIF ELSE BEGIN
                  be1 = [be1, te1]
                  be2 = [be2, te2]
                  be1sig = [be1sig, te1sig]
                  be2sig = [be2sig, te2sig]
                  be1err = [be1err, te1err]
                  be2err = [be2err, te2err]
                  be1sigerr = [be1sigerr, te1sigerr]
                  be2sigerr = [be2sigerr, te2sigerr]
                  be1errerr = [be1errerr, te1errerr]
                  be2errerr = [be2errerr, te2errerr]

                  nobj = [nobj, tnobj]
              ENDELSE 

              ime1[idec, ira] = te1
              ime2[idec, ira] = te2
              ime[idec, ira] = sqrt( te1^2 + te2^2 )
              imnum[idec, ira] = tnobj
              imra[idec, ira]=(ra1+ra2)/2.
              imdec[idec, ira]=(dec1+dec2)/2.
              
          ENDFOR 
      ENDFOR 
      ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
      ; Output the images
      ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

      IF map THEN BEGIN 
          nrastr = ntostr(nra)
          ndecstr = ntostr(ndec)
          fe1=imdir+'e1im_'+nnm+'_'+colors[clr]+nrastr+'x'+ndecstr+'.fit'
          fe2=imdir+'e2im_'+nnm+'_'+colors[clr]+nrastr+'x'+ndecstr+'.fit'
          fe = imdir+'eim_'+nnm+'_'+colors[clr]+nrastr+'x'+ndecstr+'.fit'
          fnum=imdir+'nim_'+nnm+'_'+colors[clr]+nrastr+'x'+ndecstr+'.fit'
          fra=imdir+'raim_'+nnm+'_'+colors[clr]+nrastr+'x'+ndecstr+'.fit'
          fdec=imdir+'decim_'+nnm+'_'+colors[clr]+nrastr+'x'+ndecstr+'.fit'
          print
          print,'Creating images: ',imdir+'*_'+$
                nnm+'_'+colors[clr]+nrastr+'x'+ndecstr+'.fit'
          writefits, fe1, ime1
          writefits, fe2, ime2
          writefits, fe, ime
          writefits, fnum, imnum
          writefits, fra, imra
          writefits, fdec, imdec
      ENDIF 

      ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
      ;; Get averages from the individual bins
      ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

      vobj = (moment(nobj))[1]
      tnobjavgerr = sqrt(vobj/nb)
      tnobjavg=median(nobj)
      tnobjtot=total(nobj)
      
      ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
      ; find bin to bin spread
      ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

      ve1 = (moment(be1))[1]
      ve2 = (moment(be2))[1]
      s1=sqrt(ve1)
      s2=sqrt(ve2)

      var1 = be1err^2
      var2 = be2err^2
      
      v1int = (ve1 - median(var1) ) > 0.
      v2int = (ve2 - median(var2) ) > 0.
      IF v1int EQ 0 THEN print,'Dohh1'
      IF v2int EQ 0 THEN print,'Dohh2'
      
      wmom, be1, sqrt(v1int + var1), tbe1, tbe1sig, tbe1uncert, tbe1sigerr
      wmom, be2, sqrt(v2int + var2), tbe2, tbe2sig, tbe2uncert, tbe2sigerr

      ;; Using standard Unweighted formula
      tbe1sigerr = tbe1sig/sqrt(2*nb)
      tbe2sigerr = tbe2sig/sqrt(2*nb)

      IF hist THEN BEGIN 
          plothist, be1, bin=.16*s1,xstyle=1, xrange=[-.2,.2],$
            xtitle='e1',title=title
          plothist, be2, bin=.16*s2,xstyle=1,  xrange=[-.2,.2],$
            xtitle='e2',title=title
          key=get_kbrd(20)
      ENDIF 

      ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
      ; Find mean uncertainty for each binning
      ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
      ; try doing first order thing first.

      v1 = (moment(be1err))[1]
      v2 = (moment(be2err))[1]

      s1 = sqrt(v1)
      s2 = sqrt(v2) 

      ;; Weighted 
      ; Estimate intrinsic spread for weighting
      var1 = be1errerr^2
      var2 = be2errerr^2

      v1int = (v1 - median(var1) ) > 0.
      v2int = (v2 - median(var2) ) > 0.

      IF v1int EQ 0 THEN print,'Dohh2'
      IF v2int EQ 0 THEN print,'Dohh3'

      wmom, be1err, sqrt( v1int + var1 ), tbe1err, tbe1errsig, tbe1errerr
      wmom, be2err, sqrt( v2int + var2 ), tbe2err, tbe2errsig, tbe2errerr

      ;; Unweighted formulae

;      tbe1err = median(be1err)
;      tbe2err = median(be2err)
;      tbe1errsig = s1
;      tbe2errsig = s2
;      tbe1errerr = tbe1errsig/sqrt(nb)
;      tbe2errerr = tbe2errsig/sqrt(nb)

      IF hist THEN BEGIN 
          plothist, be1err, bin=.16*s1,xrange=[0,.07],xstyle=1, $
            xtitle='e1err',title=title
          plothist, be2err, bin=.16*s2,xrange=[0,.07],xstyle=1, $
            xtitle='e2err',title=title
          key=get_kbrd(20)
      ENDIF 

      ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
      ; Approximate values (a for approximate)
      ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

      tae1err = 0.32/sqrt(tnobjavg)
      tae2err = 0.32/sqrt(tnobjavg)

      out='   '+ntostr(nb,6)+'     '+ntostr(ba,6)+'   '+ntostr(ta,6)
      out=out+'   '+ntostr(tbe1err,7)+'  '+ntostr(tbe2err,7)+'   '
      out=out+ntostr(tnobjavg,6) 
      printf, lun1, out

      IF ci EQ 0 THEN BEGIN 
          bina = ba
          nobjavg = tnobjavg
          nobjavgerr = tnobjavgerr
          nobjtot = tnobjtot

          e1 = tbe1             ; -average e1 over all bins
          e2 = tbe2
          e1uncert = tbe1uncert ; -uncertainty in this average
          e2uncert = tbe2uncert ;  Note this is VERY different from e1err which
          e1sig = tbe1sig       ;  is average uncertainty for a given bin
          e2sig = tbe2sig       ; -spread around average
          e1sigerr = tbe1sigerr ; -Error in sigma.
          e2sigerr = tbe2sigerr

          e1err = tbe1err       ; -Average uncertainty over this sized bins
          e2err = tbe2err       ;  Found uncertainty in each bin and averaged
          e1errerr = tbe1errerr
          e2errerr = tbe2errerr

          ae1err = tae1err
          ae2err = tae2err
      ENDIF ELSE BEGIN
          bina = [bina, ba]
          nobjavg = [nobjavg, tnobjavg]
          nobjavgerr = [nobjavgerr, tnobjavgerr]
          nobjtot = [nobjtot, tnobjtot]
          
          e1 = [e1, tbe1]
          e2 = [e2, tbe2]
          e1uncert = [e1uncert, tbe1uncert]
          e2uncert = [e2uncert, tbe2uncert]
          e1sig = [e1sig, tbe1sig]
          e2sig = [e2sig, tbe2sig]
          e1sigerr = [e1sigerr, tbe1sigerr]
          e2sigerr = [e2sigerr, tbe2sigerr]

          e1err = [e1err,tbe1err]
          e2err = [e2err, tbe2err]
          e1errerr = [e1errerr, tbe1errerr]
          e2errerr = [e2errerr, tbe2errerr]

          ae1err = [ae1err, tae1err]
          ae2err = [ae2err, tae2err]
      ENDELSE 
      
  ENDFOR 
      

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Sort them by bin area
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  s=sort(bina)
  bina = bina(s)

  nobjavg = nobjavg(s)
  nobjavgerr = nobjavgerr(s)
  nobjtot = nobjtot(s)
  e1 = e1[s]
  e2 = e2[s]
  e1uncert = e1uncert[s]
  e2uncert = e2uncert[s]
  e1sig = e1sig[s]
  e2sig = e2sig[s]
  e1sigerr = e1sigerr[s]
  e2sigerr = e2sigerr[s]

  e1err = e1err(s)
  e2err = e2err(s)
  e1errerr = e1errerr(s)
  e2errerr = e2errerr(s)

  ae1err = ae1err(s)
  ae2err = ae2err(s)
  
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Plot and print the results
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  mes=    '      binarea'
  mes1=mes+'         e1err        e1errerr         e2err          e2errerr'
  mes2=mes+'         e1sig        e1sigerr         e2sig          e2sigerr'
  mes3=mes+'         <e1>         e1uncert         <e2>           e2uncert'
  mes4=mes+'        <nobj>      .32/sqrt(nobj)    nobjtot'
  
  printf, lun2, mes1
  forprint, bina, $
            e1err, e1errerr, e2err, e2errerr, $
            TEXT=5, $  ;; prints to datafile ( I set !textunit = lun2)
            /silent
  printf, lun2, mes2
  forprint, bina, $
            e1sig, e1sigerr, e2sig, e2sigerr, $
            TEXT=5, $
            /silent
  printf, lun2, mes3
  forprint, bina, $
            e1, e1uncert, e2, e2uncert, $
            TEXT=5, $
            /silent
  printf, lun2, mes4
  forprint, bina, $
            nobjavg, ae1err, nobjtot, $
            TEXT=5, $
            /silent

  title = 'Runs '+r1str+' and '+r2str+'  '+nnm+' in '+colors[clr]
  xt='square degrees'

  m = max([ 1.1*max(e1err), 1.1*max(e2err),1.1*max(e1sig), 1.1*max(e2sig) ])

  ; create postscript file for output
  makeps, psname, /noland

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Linear plots
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  yt=''
  ploterr, bina, e1err, e1errerr, title=title, xtitle=xt, ytitle=yt,psym=1, $
           yrange=[0,m]
  oploterr, bina, e1sig, e1sigerr, psym=3
  oplot, bina, ae1err
  legend, ['<e1err>', 'bin2bin spread','.32/sqrt(N)'],psym=[1,3,0],$
          position=[3.,.035]

  yt=''
  ploterr, bina, e2err, e2errerr, title=title, xtitle=xt, ytitle=yt,psym=1, $
           yrange=[0,m]
  oploterr, bina, e2sig, e2sigerr, psym=3
  oplot, bina, ae2err
  legend, ['<e2err>', 'bin2bin spread','.32/sqrt(N)'],psym=[1,3,0],$
          position=[3.,.035]

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Log plots
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  yt=''
  ploterr, bina, e1err, e1errerr, title=title, xtitle=xt, ytitle=yt,psym=1, $
           /xlog, /ylog
  oploterr, bina, e1sig, e1sigerr, psym=3
  oplot, bina, ae1err
  legend, ['<e1err>', 'bin2bin spread','.32/sqrt(N)'],psym=[1,3,0],$
          position=[.6,.06]

  yt=''
  ploterr, bina, e2err, e2errerr, title=title, xtitle=xt, ytitle=yt,psym=1, $
           /xlog, /ylog
  oploterr, bina, e2sig, e2sigerr, psym=3
  oplot, bina, ae2err
  legend, ['<e2err>', 'bin2bin spread','.32/sqrt(N)'],psym=[1,3,0],$
          position=[.6,.06]
  

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Plot of <e1> and <e2> vs binsize
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  yt='<e1>'
  ploterr, bina, e1, e1uncert, title=title, xtitle=xt, ytitle=yt,psym=1

  yt='<e2>'
  ploterr, bina, e2, e2uncert, title=title, xtitle=xt, ytitle=yt,psym=1

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ; Plots of <nobjavg> and nobjtot vs binsize
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  
  yt='<nobj>'
  ploterr, bina, nobjavg, nobjavgerr, title=title, ytitle=yt, xtitle=xt,psym=1

  yt = 'Total Objects Used'
  plot, bina, nobjtot, title=title, ytitle=yt, xtitle=xt,psym=1

  ;close plot
  ep

  !p.multi=pold


  close, lun1
  free_lun, lun1
  close, lun2
  free_lun, lun2

  ptime, systime(1)-tt
  return 
END 































